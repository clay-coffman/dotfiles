###################################################################################################
# ENV Vars + Path stuff
###################################################################################################

export LANG=en_US.UTF-8
export EDITOR=nvim

export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_STATE_HOME="${XDG_STATE_HOME:-$HOME/.local/state}"
export XDG_RUNTIME_DIR="${XDG_RUNTIME_DIR:-/run/user/$(id -u)}"

export PRETTIERD_DEFAULT_CONFIG="$XDG_CONFIG_HOME/.prettierrc.yaml"
export TAPLO_CONFIG="$XDG_CONFIG_HOME/taplo.toml"
export RIPGREP_CONFIG_PATH="$XDG_CONFIG_HOME/ripgrep/config"

# Personal API keys
export GEMINI_API_KEY="{{ onepasswordRead "op://Private/Gemini API Key 1/credential" }}"
export GITHUB_TOKEN="{{ onepasswordRead "op://Private/gh_token_personal_thinkpad_1/token" }}"
export REF_API_KEY="{{ onepasswordRead "op://Private/Ref API Key/credential" }}"
export CONTEXT7_API_KEY="{{ onepasswordRead "op://Private/Context7 API Key/credential" }}"
export HETZNER_TOKEN="{{ onepasswordRead "op://Private/q4hp4mv4lefdvxbtdd2k6er6aq/credential" }}"

export PATH="$HOME/bin:$HOME/.local/bin:$PATH"

###################################################################################################
# History + Shell Options
###################################################################################################

HISTFILE="${XDG_STATE_HOME}/zsh/history"
[[ ! -d "${HISTFILE:h}" ]] && mkdir -p "${HISTFILE:h}"
HISTSIZE=10000
SAVEHIST=10000

setopt HIST_IGNORE_ALL_DUPS
setopt APPEND_HISTORY
setopt SHARE_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_VERIFY
setopt INC_APPEND_HISTORY
setopt AUTO_CD
setopt EXTENDED_GLOB
setopt CORRECT
SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

WORDCHARS=${WORDCHARS//[\/]}

###################################################################################################
# Completions
###################################################################################################

zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/zcompcache"

[[ ! -d "$XDG_CACHE_HOME/zsh" ]] && mkdir -p "$XDG_CACHE_HOME/zsh"

if [[ -d "$HOME/.zsh/completions" ]]; then
  fpath=("$HOME/.zsh/completions" $fpath)
fi

if [[ -d "$HOME/.docker/completions" ]]; then
  fpath=("$HOME/.docker/completions" $fpath)
fi

if [[ -d "$HOME/.asdf/completions" ]]; then
  fpath=("$HOME/.asdf/completions" $fpath)
fi

###################################################################################################
# Zi plugin manager
###################################################################################################

typeset -A ZI
ZI[BIN_DIR]="$HOME/.zi/bin"

if [[ -f "${ZI[BIN_DIR]}/zi.zsh" ]]; then
  source "${ZI[BIN_DIR]}/zi.zsh"
else
  print "Zi not found at ${ZI[BIN_DIR]}/zi.zsh â€“ install with: sh -c \"\$(curl -fsSL get.zshell.dev)\" --" >&2
fi

autoload -Uz _zi 2>/dev/null
(( ${+_comps} )) && _comps[zi]=_zi

###################################################################################################
# Plugins via Zi
###################################################################################################

ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT
ZVM_KEYTIMEOUT=0.40
ZVM_ESCAPE_KEYTIMEOUT=0.03
ZVM_SYSTEM_CLIPBOARD_ENABLED=true
ZVM_INIT_MODE=sourcing

zi ice depth=1
zi light jeffreytse/zsh-vi-mode

zi ice wait lucid has'zoxide'
zi light z-shell/zsh-zoxide

zi ice wait lucid
zi snippet OMZ::plugins/git/git.plugin.zsh

zi ice wait lucid
zi snippet OMZ::plugins/alias-finder/alias-finder.plugin.zsh

if (( $+functions[zicompinit] )); then
  zicompinit
  zicdreplay
else
  autoload -Uz compinit
  compinit -C
fi

###################################################################################################
# FZF
###################################################################################################

if command -v fzf >/dev/null 2>&1; then
  export FZF_DEFAULT_COMMAND='rg --files --hidden --follow --glob "!.git/*"'
  export FZF_DEFAULT_OPTS="--height 40% --reverse --border --preview 'bat --color=always {}' --preview-window=right:60%:wrap"
  export FZF_CTRL_T_OPTS="--preview '(bat --color=always {} 2> /dev/null || tree -C {}) 2> /dev/null | head -200'"
  export FZF_ALT_C_OPTS="--preview 'tree -C {} | head -200'"

  # Load fzf keybindings + completion
  source <(fzf --zsh)
fi

###################################################################################################
# asdf
###################################################################################################

if [[ -f "$HOME/.asdf/asdf.sh" ]]; then
  source "$HOME/.asdf/asdf.sh"
fi

###################################################################################################
# Custom Aliases & Functions
###################################################################################################

alias v="nvim"
alias c="clear"
alias l="ls -la"
alias cat="bat"
alias bake="mbake"
alias open='xdg-open'
alias update='sudo dnf update'
alias search='dnf search'
alias install='sudo dnf install'

# clipboard
if command -v wl-copy &> /dev/null; then
    alias pbcopy='wl-copy'
    alias pbpaste='wl-paste'
elif command -v xclip &> /dev/null; then
    alias pbcopy='xclip -selection clipboard'
    alias pbpaste='xclip -selection clipboard -o'
fi

# initializes project mcp file with base mcp servers
mcp-init() {
    local project_dir="${1:-.}"
    cd "$project_dir" || return 1
    ~/.config/scripts/merge-claude-config.sh
    if [[ -n "$2" ]]; then
        echo "Add project-specific servers to .mcp.json"
    fi
}

# change CWD on quit from yazi (q) or (Q) to stay put
y() {
    local tmp cwd
    tmp="$(mktemp -t "yazi-cwd.XXXXXX")"
    yazi "$@" --cwd-file="$tmp"
    IFS= read -r -d '' cwd < "$tmp"
    [[ -n "$cwd" && "$cwd" != "$PWD" ]] && builtin cd -- "$cwd"
    rm -f -- "$tmp"
}

###################################################################################################
# Prompt tooling
###################################################################################################

# Starship prompt
if command -v starship >/dev/null 2>&1; then
  eval "$(starship init zsh)"
fi

# Atuin (shell history)
if command -v atuin >/dev/null 2>&1; then
  eval "$(atuin init zsh)"
fi
